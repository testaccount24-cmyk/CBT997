/* --------------------  rexx procedure  -------------------- *
 | Name:      zigimrgm                                        |
 |                                                            |
 | Function:  ISPF Edit Macro to:                             |
 |                                                            |
 |   Parm M: remove all non-merge records                     |
 |   Parm blank: remove all merge records                     |
 |               and compare merge-file x                     |
 |                                                            |
 | Usage Notes: called by ZIGI                                |
 |                                                            |
 | Author:    Lionel B. Dyck                                  |
 |                                                            |
 | History:  (most recent on top)                             |
 |            01/08/20 LBD - add msgline comments             |
 |            12/08/19 LBD - creation                         |
 |                                                            |
 | ---------------------------------------------------------- |
 |    ZIGI - the z/OS ISPF Git Interface                      |
 |    Copyright (C) 2020 - Henri Kuiper and Lionel Dyck       |
 |                                                            |
 |    This program is free software: you can redistribute it  |
 |    and/or modify it under the terms of the GNU General     |
 |    Public License as published by the Free Software        |
 |    Foundation, either version 3 of the License, or (at     |
 |    your option) any later version.                         |
 |                                                            |
 |    This program is distributed in the hope that it will be |
 |    useful, but WITHOUT ANY WARRANTY; without even the      |
 |    implied warranty of MERCHANTABILITY or FITNESS FOR A    |
 |    PARTICULAR PURPOSE.  See the GNU General Public License |
 |    for more details.                                       |
 |                                                            |
 |    You should have received a copy of the GNU General      |
 |    Public License along with this program.  If not, see    |
 |    <https://www.gnu.org/licenses/>.                        |
 * ---------------------------------------------------------- */
  Address ISREdit
  'Macro (options)'
  'reset all'
  'recovery on'
  address ispexec 'vget (mdsn mrgdir mrgfile)'

  if options = '' then do
    '(last) = linenum .zlast'
    hit = 0
    do i = last to 1 by -1
      '(data) = line' i
      Select
      when pos('<<<<<<<',data) > 0 then do
        'delete' i
        hit = 0
      end
      when pos('=======',data) > 0 then do
        'delete' i
        hit = 0
      end
      when pos('>>>>>>>',data) > 0 then do
        'delete' i
        hit = 1
      end
      when hit = 1 then do
        'delete' i
      end
      otherwise nop
    end
    end
    'Compare' mrgfile 'x'
    'locate first excluded'
    '(line) = cursor'
    text = 'Or, use the CANCEL command to terminate the merge.'
    'line_after' line '= msgline (text)'
    text = 'Then Save and End to update.'
    'line_after' line '= msgline (text)'
    text = 'using the MD (make data) Edit line command.'
    'line_after' line '= msgline (text)'
    text = 'Update this data by deleting records or converting' ,
           'the deleted records'
    'line_after' line '= msgline (text)'
    'Cursor = 1'
    Exit 1
  end

  if options = 'M' then do
    '(last) = linenum .zlast'
    hit = 0
    do i = last to 1 by -1
      '(data) = line' i
      Select
      when pos('<<<<<<<',data) > 0 then do
        'delete' i
        hit = 1
      end
      when pos('=======',data) > 0 then do
        'delete' i
        hit = 1
      end
      when pos('>>>>>>>',data) > 0 then do
        'delete' i
        hit = 0
      end
      when hit = 1 then do
        'delete' i
      end
      otherwise nop
    end
    end
    'End'
    exit
  end

  exit
